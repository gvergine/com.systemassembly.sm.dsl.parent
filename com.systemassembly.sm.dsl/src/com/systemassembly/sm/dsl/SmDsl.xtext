grammar com.systemassembly.sm.dsl.SmDsl with org.eclipse.xtext.common.Terminals

generate smDsl "http://www.systemassembly.com/sm/dsl/SmDsl"

Model:
	'name' name=ID
	eventsSection=EventsSection
	commandsSection=CommandsSection
	states+=State*
	;

	
EventsSection:
	{EventsSection} 'events' '{'
	events+=Event*
	'}'
;

Event:
	name=ID
;


CommandsSection:
	{CommandsSection} 'commands' '{'
	commands+=Command*
	'}'
;


Command:
	name=ID
;


State:
	initial?='initial'? 'state' name=ID '{'
	('on' 'entry' '{'
	('exec' entryCommands+=[Command] (',' entryCommands+=[Command])*)?
	'}')?
	eventHandlingDescriptions+=EventHandlingDescription*
	('on' 'exit' '{'
	('exec' exitCommands+=[Command] (',' exitCommands+=[Command])*)?
	'}')?
	'}'
;

EventHandlingDescription:
	'on' event=[Event] '{'
	('exec' commands+=[Command] (',' commands+=[Command])*)?
	('goto' targetState=[State])?
	'}'
;


