grammar com.systemassembly.sm.dsl.SmDsl with org.eclipse.xtext.common.Terminals

generate smDsl "http://www.systemassembly.com/sm/dsl/SmDsl"

Model:
	'name' name=ID
	eventsSection=EventsSection
	statesSection=StatesSection
	commandsSection=CommandsSection
	stateDescriptions+=StateDescription*
	;

	
EventsSection:
	{EventsSection} 'events' '{'
	events+=Event*
	'}'
;

Event:
	name=ID
;

StatesSection:
	{StatesSection} 'states' '{'
	states+=State*
	'}'
;

State:
	name=ID
;

CommandsSection:
	{CommandsSection} 'commands' '{'
	commands+=Command*
	'}'
;


Command:
	name=ID
;


StateDescription:
	initial?='initial'? 'state' state=[State] '{'
	('on' 'entry' '{'
	('exec' entryCommands+=[Command] (',' entryCommands+=[Command])*)?
	'}')?
	eventHandlingDescriptions+=EventHandlingDescription*
	('on' 'exit' '{'
	('exec' exitCommands+=[Command] (',' exitCommands+=[Command])*)?
	'}')?
	'}'
;

EventHandlingDescription:
	'on' event=[Event] '{'
	('exec' commands+=[Command] (',' commands+=[Command])*)?
	('goto' targetState=[State])?
	'}'
;


